import jaci.openrio.gradle.GradleRIOPlugin
import jaci.openrio.gradle.frc.FRCJavaArtifact
import jaci.openrio.gradle.frc.RoboRIO

plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'jaci.openrio.gradle.GradleRIO' version '2018.01.22'
}

def TEAM = 5431
def ROBOT_CLASS = "frc.team5431.robot.Robot"

sourceCompatibility = 1.8

configurations.all {
    //Check for updates of selected packages every minute (Only checked when built [don't worry])
    resolutionStrategy.cacheChangingModulesFor 60, 'seconds'
}

// Targets (RoboRIO) and artifacts (deployable files)
deploy {
    targets {
        target("roborio", RoboRIO) {
            team = TEAM
        }
    }
    artifacts {
        artifact('frcJava', FRCJavaArtifact) {
            targets << "roborio"
        }
    }
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

// WPILib (+ friends), CTRE Phoenix (Talon SRX) and NavX.
dependencies {
    compile wpilib()
    compile ctre()
    compile navx()
    compile openrio.powerup.matchData()
    compile('org.codehaus.groovy:groovy-all:2.3.11')
    compile('com.github.frc5431:TitanUtil:1.1.2') { changing = true } //Check for updates
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

//Add all of the libraries into one jar
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}
